#### python modules ####

import glob
import os

#### CONFIG FILE ####
#which datasets are to be aligned to which hosts and viruses are specified in dsets.yaml
#also whether or not to try to merge R1 and R2
configfile: "dsets.yaml"

#this is of the form { dset1: {host: host1, virus: host2, merge: True}, dset2: {host: host2, virus: virus2, merge: False} }

#make four lists, with each entry in each list corresponding to one desired output file

DATASETS = []
SAMPLES = []
HOSTS = []
VIRUSES = []
#also make dictionary MERGE, with key dataset_sample and value True or False
MERGE = {}

for dataset in config:
	#get files in directory and strip off "_1.fastq.gz"
	samples = [os.path.basename(f)[:-11] for f in glob.glob("../data/reads/{}/*_1.fastq.gz".format(dataset))]
	for sample in samples:
		DATASETS.append(dataset)
		SAMPLES.append(sample)
		HOSTS.append(config[dataset]["host"])
		VIRUSES.append(config[dataset]["virus"])
		MERGE["{}_{}".format(dataset, sample)] = config[dataset]["merge"]

#### target files ####
rule all:
	input: 
		"../out/summary/count_mapped.xlsx",
		"../out/summary/num_sites.xlsx",
		expand("../out/{dset}/ints/{samp}.{host}.{virus}.soft.merged.bed", zip, dset = DATASETS, samp = SAMPLES, host = HOSTS, virus = VIRUSES)
		

#### merging and dedup ###

rule seqPrep:
	input:
		r1 = "../data/reads/{dset}/{samp}_1.fastq.gz",
		r2 = "../data/reads/{dset}/{samp}_2.fastq.gz"	
	output:
		merged = "../out/{dset}/merged_reads/{samp}.SeqPrep_merged.fastq.gz",
		proc_r1 = "../out/{dset}/merged_reads/{samp}.SeqPrep_R1.fastq.gz",
		proc_r2 = "../out/{dset}/merged_reads/{samp}.SeqPrep_R2.fastq.gz",
		all = "../out/{dset}/merged_reads/{samp}.all.fastq.gz"
	conda:	
		"envs/seqprep.yml"
	shell:
		"""
		SeqPrep -f {input.r1} -r {input.r2} -1 {output.proc_r1} -2 {output.proc_r2} -s {output.merged}
		cat {output.proc_r1} {output.proc_r2} {output.merged} > {output.all}
		"""

rule combine:
	input:
		r1 = "../data/{dset}/reads/{samp}_1.fastq.gz",
		r2 = "../data/{dset}/reads/{samp}_2.fastq.gz"	
	output:
		proc_r1 = "../out/{dset}/combined_reads/{samp}.R1.fastq.gz",
		proc_r2 = "../out/{dset}/combined_reads/{samp}.R2.fastq.gz",
		all = "../out/{dset}/combined_reads/{samp}.all.fastq.gz"
	shell:
		"""
		cp {input.r1} {output.proc_r1}
		cp {input.r2} {output.proc_r2}
		cat {input.r1} {input.r2} > {output.all}
		"""

#functions for if we did seqPrep or not

def get_all(wildcards):
	if MERGE["{}_{}".format(wildcards.dset, wildcards.samp)] == "True":
		folder = "merged_reads"
	else:
		folder = "combined_reads"
	return "../out/{}/{}/{}.all.fastq.gz".format(wildcards.dset, folder, wildcards.samp)

def get_r1(wildcards):
	if MERGE["{}_{}".format(wildcards.dset, wildcards.samp)] == "True":
		typeRead = "seqPrep_R1"
		folder = "merged_reads"
	else:
		typeRead = "R1"	
		folder = "combined_reads"
	return "../out/{}/{}/{}.{}.fastq.gz".format(wildcards.dset, folder, wildcards.samp, typeRead)

def get_r2(wildcards):
	if MERGE["{}_{}".format(wildcards.dset, wildcards.samp)] == "True":
		typeRead = "seqPrep_R2"
		folder = "merged_reads"
	else:
		typeRead = "R2"	
		folder = "combined_reads"
	return "../out/{}/{}/{}.{}.fastq.gz".format(wildcards.dset, folder, wildcards.samp, typeRead)


rule dedup:
	input:
		all = get_all,
		proc_r1 = get_r1,
		proc_r2 = get_r2
	output:
		all_clumped = "../out/{dset}/dedup_reads/{samp}.all.fastq.gz",
		R1_clumped = "../out/{dset}/dedup_reads/{samp}.R1.fastq.gz",
		R2_clumped = "../out/{dset}/dedup_reads/{samp}.R2.fastq.gz"
	conda:
		"envs/bbmap.yml"
	shell:
		"""
		clumpify.sh in={input.all} out={output.all_clumped} dedupe
		clumpify.sh in={input.proc_r1} in2={input.proc_r2} out={output.R1_clumped} out2={output.R2_clumped} dedupe
		"""


#### alignments ####

rule index:
	input:
		"../data/references/{genome}.fa"
	output:
		"../data/references/{genome}.ann",
		"../data/references/{genome}.amb",
		"../data/references/{genome}.bwt",
		"../data/references/{genome}.pac",
		"../data/references/{genome}.sa"
	conda: 
		"envs/bwa.yml"
	shell:
		"bwa index -p ../data/references/{wildcards.genome} {input}"


rule align_bwa_virus:
	input:
		ann = "../references/{virus}.ann",
		amb = "../references/{virus}.amb",
		bwt = "../references/{virus}.bwt",
		pac = "../references/{virus}.pac",
		sa = "../references/{virus}.sa",
		all = "../out/{dset}/dedup_reads/{samp}.all.fastq.gz",
		r1 = "../out/{dset}/dedup_reads/{samp}.R1.fastq.gz",
		r2 = "../out/{dset}/dedup_reads/{samp}.R2.fastq.gz" 
	output:
		vPaired = temp("../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.sam"),
		vSing = temp("../out/{dset}/virus_aligned/{samp}.{virus}.bwa.sam")
	conda: 
		"envs/bwa.yml"
	threads: 5
	shell:	
		"""
		python ./alignReadsWithBWA.py --threads {threads} --index ../data/references/{wildcards.virus} --read1 {input.r1} --read2 {input.r2} --output {output.vPaired} --threshold 10 --hflag 200
		python ./alignReadsWithBWA.py --threads {threads} --index ../data/references/{wildcards.virus} --read1 {input.all} --output {output.vSing} --threshold 10 --hflag 200
		"""

rule convert:
	input:
		"../out/{dset}/{host_virus}/{samp}.{host_virus_name}.{alignType}.sam"
	output:
		bam = "../out/{dset}/{host_virus}/{samp}.{host_virus_name}.{alignType}.bam",
		bai = "../out/{dset}/{host_virus}/{samp}.{host_virus_name}.{alignType}.bam.bai"
	conda: 
		"envs/bwa.yml"	
	shell:
		"""
		samtools view -bhS {input} | samtools sort - -o {output.bam}
		samtools index {output.bam}
		"""

rule extract_vAligned:
	input:
		vSing = "../out/{dset}/virus_aligned/{samp}.{virus}.bwa.bam",
		vPaired = "../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.bam"
	output:
		svSam = "../out/{dset}/virus_aligned/{samp}.{virus}.bwa.mapped.sam",
		pvBam_readMap_mateUnmap = "../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.mapped1.bam",
		pvBam_readUnmap_mateMap = "../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.mapped2.bam",
		pvBam = "../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.mapped.bam",
		pvSam = "../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.mapped.sam"
	conda:
		"envs/bwa.yml"
	shell:
		"""
		samtools view -h -F 0x4 -F 0x800 -o {output.svSam} {input.vSing} 
		samtools view -hb -F 0x4 -f 0x8 -F 0x800 -o {output.pvBam_readMap_mateUnmap} {input.vPaired}
		samtools view -hb -f 0x4 -F 0x8 -F 0x800 -o {output.pvBam_readUnmap_mateMap} {input.vPaired}
		samtools merge {output.pvBam} {output.pvBam_readMap_mateUnmap} {output.pvBam_readUnmap_mateMap}
		samtools view -h -o {output.pvSam} {output.pvBam}
		"""

rule extract_vAligedtoFastq:
	input: 
		svBam = "../out/{dset}/virus_aligned/{samp}.{virus}.bwa.mapped.bam",
		pvBam = "../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.mapped.bam"
	output:
		svFastq = "../out/{dset}/reads/{samp}.bwa.mappedTo{virus}.fastq.gz",
		pvFastq1 = "../out/{dset}/reads/{samp}.bwaPaired.mappedTo{virus}_1.fastq.gz",
		pvFastq2 = "../out/{dset}/reads/{samp}.bwaPaired.mappedTo{virus}_2.fastq.gz"
	conda:
		"envs/picard.yml"
	shell:
		"""
		java -jar picard.jar SamToFastq I={input.svBam} FASTQ={output.svFastq}
		java -jar picard.jar SamToFastq I={input.pvBam} FASTQ={output.pvFastq1} SECOND_END_FASTQ={output.pvFastq2}
		"""

rule align_bwa_host:
	input:	
		ann = "../data/references/{host}.ann",
		amb = "../data/references/{host}.amb",
		bwt = "../data/references/{host}.bwt",
		pac = "../data/references/{host}.pac",
		sa = "../data/references/{host}.sa",
		all = "../out/{dset}/reads/{samp}.bwa.mappedTo{virus}.fastq.gz",
		r1 = "../out/{dset}/reads/{samp}.bwaPaired.mappedTo{virus}_1.fastq.gz",
		r2 = "../out/{dset}/reads/{samp}.bwaPaired.mappedTo{virus}_2.fastq.gz"

	output:
		hPaired = temp("../out/{dset}/host_aligned/{samp}.{host}.bwaPaired.{virus}Mappedreads.sam"),
		hSing = temp("../out/{dset}/host_aligned/{samp}.{host}.bwa.{virus}Mappedreads.sam")
	conda: 
		"envs/bwa.yml"
	threads: 5
	shell:		
		"""
		python ./alignReadsWithBWA.py --threads {threads} --index ../references/{wildcards.host} --read1 {input.r1} --read2 {input.r2} --output {output.hPaired} --threshold 10 --hflag 200
		python ./alignReadsWithBWA.py --threads {threads} --index ../references/{wildcards.host} --read1 {input.all} --output {output.hSing} --threshold 10 --hflag 200
		"""


#### perl scripts ####

rule run_int_scripts:
	input:
		hPaired = "../out/{dset}/host_aligned/{samp}.{virus}Mappedreads.{host}.bwaPaired.sam",
		hSing = "../out/{dset}/host_aligned/{samp}.{virus}Mappedreads.{host}.bwa.sam",
		vPaired = "../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.mapped.sam",
		vSing = "../out/{dset}/virus_aligned/{samp}.{virus}.bwa.mapped.sam"
	output:
		allInt = "../out/{dset}/ints/{samp}.{host}.{virus}.integrations.txt",
		soft = temp("../out/{dset}/ints/{samp}.{host}.{virus}.soft.txt"),
		discord = temp("../out/{dset}/ints/{samp}.{host}.{virus}.discordant.txt"),
		allIntTemp = temp("../out/{dset}/ints/{samp}.{host}.{virus}.integrations.txt.tmp"),
		softBed = "../out/{dset}/ints/{samp}.{host}.{virus}.soft.bed",
		mergedBed = "../out/{dset}/ints/{samp}.{host}.{virus}.soft.merged.bed"
	conda:
		"envs/bedtools.yml"

	shell:
		"""
		perl -I . ./softClip.pl --viral {input.vSing} --human {input.hSing} --output {output.soft} --bed {output.softBed} --merged {output.mergedBed}
		perl -I . ./discordant.pl --viral {input.vPaired} --human {input.hPaired} --output {output.discord} 
		awk 'FNR==1 && NR!=1 {{ getline; }} {{ print $0; }}' {output.soft} {output.discord} > {output.allInt}
		sed -i 's\chr\ \g' {output.allInt}
		sort -n -k1,1 -k2,2 {output.allInt} > {output.allIntTemp}
		cp {output.allIntTemp} {output.allInt}
		"""
		
rule run_rearrange_scripts:
	input:
		"../out/{dset}/virus_aligned/{samp}.{virus}.bwa.sam"
	output:
		txt = "../out/{dset}/rearrange/{samp}.{virus}.rearrange.txt",
		bed = "../out/{dset}/rearrange/{samp}.{virus}.rearrange.bed"
	conda: 
		"envs/bwa.yml"
	shell:
		"perl -I . ./checkVecRearrange.pl --viral {input} --output {output.txt} --bed {output.bed}"

#### visualization ####

rule count_mapped:
	input:
		expand("../out/{dset}/host_aligned/{samp}.{host}.bwa.bam", zip, dset = DATASETS, samp = SAMPLES, host = HOSTS),
		expand("../out/{dset}/virus_aligned/{samp}.{virus}.bwa.bam", zip, dset = DATASETS, samp = SAMPLES, virus = VIRUSES),
		expand("../out/{dset}/host_aligned/{samp}.{host}.bwaPaired.bam", zip, dset = DATASETS, samp = SAMPLES, host = HOSTS),
		expand("../out/{dset}/virus_aligned/{samp}.{virus}.bwaPaired.bam", zip, dset = DATASETS, samp = SAMPLES, virus = VIRUSES)
	output:
		"../out/summary/count_mapped.txt"
	conda: 
		"envs/bwa.yml"
	shell:
		"./count_mapped.sh"

rule plot_mapped:
	input:
		"../out/summary/count_mapped.txt"
	output:
		"../out/summary/count_mapped.xlsx"
	conda:
		"envs/rscripts.yml"
	script:
		"count_reads.R"
	
rule summarise:
	input: 
		expand("../out/{dset}/ints/{samp}.{host}.{virus}.integrations.txt", zip, dset = DATASETS, samp = SAMPLES, host = HOSTS, virus = VIRUSES) 
	output:
		"../out/summary/num_sites.xlsx"
	conda:
		"envs/rscripts.yml"
	script:
		"summarise_ints.R"

		
